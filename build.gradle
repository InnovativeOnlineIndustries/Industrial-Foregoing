buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    configurations {
        classpath.resolutionStrategy {
            cacheDynamicVersionsFor 10, 'seconds'
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.1.0"
        //classpath 'com.selesse:gradle-git-changelog:0.2.+'
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.14.0"
    id 'java'
    id 'maven-publish'
    id "com.matthewprenger.cursegradle" version "1.1.0"
}
//apply plugin: 'com.selesse.git.changelog'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

def BUILD_NUMBER = System.getenv("COMMIT") ? "${project.api_version}-${System.getenv("COMMIT")}" : "${project.api_version}"
group = "com.buuz135"
archivesBaseName = "industrial-foregoing"
version = "${minecraftVersion}-$BUILD_NUMBER"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    mappings channel: 'snapshot', version: '20200129-1.15.1'
    runs {
        client = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            args '--mod', 'industrialforegoing', '--all', '--output', '"' + rootProject.file('src/generated/resources/') + '"', '--existing', '"' + sourceSets.main.resources.srcDirs[0] + '"'
            source sourceSets.main
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://maven.tterrag.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url 'https://cdn.hrzn.studio/maven/'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.1.43'
    if (findProject(':titanium') != null) {
        compile project(':titanium')
    } else {
        compile fg.deobf (project.dependencies.create('com.hrznstudio:titanium:1.15.2-2.3.+'))
    }
    //if (findProject(':workspace') == null) {
        compileOnly fg.deobf("mezz.jei:jei-1.15.2:6.0.0.2:api")
        // at runtime, use the full JEI jar
        runtimeOnly fg.deobf("mezz.jei:jei-1.15.2:6.0.0.2")
    //}
}

afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "2000"
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', '1.13'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'META_INF/mods.toml'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion': '1.13'
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META_INF/mods.toml'
    }
}

task apiJar(type: Jar, dependsOn: 'classes') {
    classifier = 'api'
    from(sourceSets.main.output) {
        include 'com/buuz135/industrial/api/**'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task deobfJar(type:Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

artifacts {
    archives sourcesJar
    archives deobfJar
    archives apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact deobfJar
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "s3://cdn.hrzn.studio/maven"
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}


tasks.curseforge.enabled =  project.hasProperty('curse_api')

curseforge {
    if (project.hasProperty('curse_api')) {
        apiKey = project.curse_api
    }
    project {
        id = '266515'
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = 'alpha'

        mainArtifact(jar){
            relations {
                requiredLibrary 'titanium'
            }
        }
        
        addArtifact sourcesJar
        addArtifact deobfJar
        addArtifact apiJar
    }
}

license {
    header rootProject.file('HEADER')
    include "**/*.java"
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Buuz135'
    ext.project = "Industrial Foregoing"
    ignoreFailures = true
    mapping {
        java='SLASHSTAR_STYLE'
    }
}
jar {
    manifest {
        attributes([
                "Specification-Title": "Industrial Foregoing",
                "Specification-Vendor": "Buuz135",
                "Specification-Version": "2.0", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.api_version}",
                "Implementation-Vendor" :"Buuz135",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
//changelog {
//    title = "Industrial Foregoing"
//    fileName = "CHANGELOG.md"
//    outputDirectory = file("$projectDir")
//    
//    commitFormat = '%s (%an)'
//    markdown {
//        commitFormat = '* %s (%an)'
//    }
//    includeLines = {
//        !it.contains("Merge")
//    }
//    processLines = {
//        String input = it as String
//        if (input.contains('[ci skip] ')) {
//            input = input.minus('[ci skip] ')
//        }
//        input
//    }
//}
